<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx" 
		  xmlns:label="skins.skin.label.*"
		  width="100%" height="100%"
		  >
	
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.com.proquifa.proquifanet.rsl.vista.utils.alertaSingleton;
			import mx.com.proquifa.vista.eventos.RegistarArribo.EventoRegistrarArribo;
			
			import skins.catalogos.catalogoIconosBotones;
			
			[Bindable] private var sizeFontTitulo:Number;
			[Bindable] private var sizeFont38:Number;
			[Bindable] private var imgH:Number;
			[Bindable] private var imgW:Number;
			[Bindable] private var altoCarrusel:Number;
			[Bindable] private var indexCarrousel:int = 0;
			
			[Bindable] public var arregloImagenes:ArrayCollection = new ArrayCollection();
			
			private var numeroMax:Number;
			private var file:File;
			
			public function clear():void
			{
				arregloImagenes = new ArrayCollection();
				indexCarrousel = 0;
				escalaImg = 1;
				grpControles.visible = false;
				imageCarrusel.visible = false;
				imageCarrusel.source = null;
				lblMarcaAgua.visible = true;
				imgBasura.visible = false;
			}
			
			override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void
			{
				super.updateDisplayList(unscaledWidth,unscaledHeight);
				
				sizeFontTitulo = (20/2005) * (unscaledWidth);
				sizeFont38 = (38/2005) * (unscaledWidth);
				imgW = (216/2005) * (unscaledWidth);
				imgH = (185/1256) * (unscaledHeight);
				altoCarrusel = (780/1256) * (unscaledHeight);
				
				imgW = imgW > 216 ? 216:imgW;
				imgH = imgH > 185 ? 185:imgH;
				altoCarrusel = altoCarrusel > 780 ? 780:altoCarrusel;
				
				sizeFontTitulo = sizeFontTitulo > 22 ? 22:sizeFontTitulo;
				sizeFontTitulo = sizeFontTitulo < 12 ? 12:sizeFontTitulo;
				
				sizeFont38 = sizeFont38 > 38 ? 38:sizeFont38;
				sizeFont38 = sizeFont38 < 18 ? 18:sizeFont38;
			}
			
			
			
			protected function cargarImagen(event:MouseEvent):void
			{
				var docFilter:FileFilter;
				file = new File();
				file.addEventListener(Event.SELECT, selectHandler);
				file.addEventListener(IOErrorEvent.IO_ERROR,ioErrorHandler);
				file.addEventListener(ProgressEvent.PROGRESS,progressHandler);
				file.addEventListener(Event.COMPLETE,completeHandler);
				docFilter = new FileFilter("Documents", "*.jpg;*.png");
				file.browse([docFilter]);
			}
			
			private function selectHandler(event:Event):void
			{
				var file:FileReference = FileReference(event.target);
				file.load();
			}
			
			private function ioErrorHandler(event:IOErrorEvent):void
			{
				trace("ioErrorHandler: " + event);
			}
			
			private function progressHandler(event:ProgressEvent):void
			{
				var file:FileReference = FileReference(event.target);
			}
			
			private function completeHandler(event:Event):void
			{
				try
				{
					var imagen:ByteArray = event.target.data;
					cargarImagenArreglo(imagen);
				}
				catch (error:*){
					alertaSingleton.show("No se encontro archivo");
				}
			}
			
			
			private function cargarImagenArreglo($imagen:ByteArray):void
			{
				if(!arregloImagenes)
					arregloImagenes = new ArrayCollection();
				
				arregloImagenes.addItem($imagen);
				
				numeroMax = arregloImagenes.length;
				indexCarrousel = arregloImagenes.length - 1;
				imageCarrusel.source = arregloImagenes[indexCarrousel];
				
				imageCarrusel.visible = true;
				grpControles.visible = true;
				imgBasura.visible = true;
				lblMarcaAgua.visible = false;
				visualizarNavegador();
				
				var update:EventoRegistrarArribo = new EventoRegistrarArribo(EventoRegistrarArribo.VALIDAR_ACTUALIZAR_BOTONERA);
				update.vieneDe = "cargarImagen";
				update.validado = true;
				dispatchEvent(update);
			}
			
			
			protected function visualizarNavegador():void
			{
				if(arregloImagenes.length > 0)
				{
					if(arregloImagenes.length > 1)
					{
						imgHojaMas.visible = imgHojaMas.includeInLayout = true;
						imgHojaMenos.visible = imgHojaMenos.includeInLayout = true;
					}
					else
					{
						imgHojaMas.visible = imgHojaMas.includeInLayout = false;
						imgHojaMenos.visible = imgHojaMenos.includeInLayout = false;
					}
				}
				else
				{
					imgHojaMas.visible = imgHojaMas.includeInLayout = false;
					imgHojaMenos.visible = imgHojaMenos.includeInLayout = false;
				}
			}
			
			protected function restarHoja():void
			{
				if(indexCarrousel > 0)
				{
					indexCarrousel--;
					imageCarrusel.source = arregloImagenes[indexCarrousel];
				}
			}
			
			protected function aumentarHoja():void
			{
				if(indexCarrousel < numeroMax - 1)
				{
					indexCarrousel++;
					imageCarrusel.source = arregloImagenes[indexCarrousel];
				}
			}
			
			
			
			private var escalaImg:Number = 1;
			public function incrementaZoom():void
			{
				if(escalaImg < 1.5)
					escalaImg = escalaImg  + .05;
				imageCarrusel.scaleX = escalaImg; 
				imageCarrusel.scaleY = escalaImg;
			}
			
			public function decrementaZoom():void
			{
				if(escalaImg > .10)
					escalaImg = escalaImg - .05;
				imageCarrusel.scaleX = escalaImg;
				imageCarrusel.scaleY = escalaImg;
			}
			
			
			
			protected function eliminarImagen(event:MouseEvent):void
			{
				if(arregloImagenes.length > 0)
				{
					arregloImagenes.removeItemAt(indexCarrousel);
					imageCarrusel.scaleX = 1;
					imageCarrusel.scaleY = 1;
					escalaImg = 1;
					
					if(arregloImagenes.length > 0)
					{
						imageCarrusel.source = arregloImagenes[0];
					}
					else
					{
						imageCarrusel.visible = false;
						imgBasura.visible = false;
						grpControles.visible = false;
						lblMarcaAgua.visible = true;
						
						var update:EventoRegistrarArribo = new EventoRegistrarArribo(EventoRegistrarArribo.VALIDAR_ACTUALIZAR_BOTONERA);
						update.vieneDe = "cargarImagen";
						update.validado = false;
						dispatchEvent(update);
					}
					numeroMax = arregloImagenes.length;
					indexCarrousel = 0;
				}
			}
			
		]]>
	</fx:Script>
	
	<s:Group width="100%" height="100%" >
		<s:Rect width="100%" height="100%">
			<s:fill>
				<s:SolidColor color="#f3f3f4" />
			</s:fill>
		</s:Rect>
		
		<s:Group id="lblMarcaAgua" width="100%" height="100%" >
			<s:VGroup id="lblInfo" horizontalCenter="0" verticalCenter="0" gap="10" horizontalAlign="center">
				<mx:Image source="{catalogoIconosBotones.getInstance().cargar_evidencias}" width="{imgW}" height="{imgH}" />
				<label:Texto_14_Helvetica text=".png / .jpg" fontSize="{sizeFontTitulo}" color="#424242"/>
			</s:VGroup>
			<label:Titulo_15_Helvetica horizontalCenter="0" verticalCenter="{lblInfo.height}" text="FotografÃ­a de paquete abierto." fontSize="{sizeFont38}" color="#888888"/>
		</s:Group>  
		
		<s:VGroup width="100%" height="75%" >
		<mx:Image id="imageCarrusel"  height="100%" width="100%" maxHeight="{altoCarrusel}" visible="false" horizontalCenter="0" verticalCenter="0"/>
		</s:VGroup>  
		
		
		
		
		<s:Group id="grpControles" width="100%"  height="10%" bottom="0" visible="false">
			<s:VGroup width="100%" height="100%" >
				<s:VGroup width="100%" height="100%" paddingBottom="10">
					<s:HGroup gap="0" width="100%" horizontalAlign="center" verticalAlign="middle">
						<s:Group click="restarHoja()">
							<mx:Image id="imgHojaMenos" source="{catalogoIconosBotones.getInstance().pagina_anterior}"  smoothBitmapContent="true" visible="false"/>
						</s:Group>
						<s:Spacer width="10" />
						<label:Texto_12 text="{' '+(indexCarrousel + 1).toString() + ' / ' + arregloImagenes.length.toString()+' '}" paddingTop="0"/>
						<s:Spacer width="10" />
						<s:Group click="aumentarHoja()">
							<mx:Image id="imgHojaMas" source="{catalogoIconosBotones.getInstance().pagina_siguiente}"  smoothBitmapContent="true" visible="false"/>
						</s:Group>
					</s:HGroup>
					<s:Spacer height="15%" />
					<s:HGroup gap="5" width="100%" horizontalAlign="center">
						<s:Group click="decrementaZoom()">
							<mx:Image id="imgZoomMenos" source="{catalogoIconosBotones.getInstance().zoom_menos}"  smoothBitmapContent="true" />
						</s:Group>
						<s:Group click="incrementaZoom()">
							<mx:Image id="imgZoomMas" source="{catalogoIconosBotones.getInstance().zoom_mas}"  smoothBitmapContent="true"/>
						</s:Group>
					</s:HGroup>
				</s:VGroup>  
				<s:Spacer height="15%" />					
			</s:VGroup>  
		</s:Group>  
		

		
		<mx:Image source="{catalogoIconosBotones.getInstance().basuraCuadro}" id="imgBasura" bottom="20" left="20" click="eliminarImagen(event)" visible="false"/>
		<mx:Image source="{catalogoIconosBotones.getInstance().agregar_foto}" bottom="20" right="20" click="cargarImagen(event)"/>
		
	</s:Group>  
	
</s:VGroup>
