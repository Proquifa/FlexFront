<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 width="100%" height="100%"
		 creationComplete="PanelContactosAnimado_creationCompleteHandler(event)"
		 >
	
	<fx:Script>
		<![CDATA[
			import com.asfusion.mate.events.DispatcherEvent;
			
			import mx.collections.ArrayCollection;
			import mx.com.proquifa.proquifanet.rsl.vista.eventos.catalogos.clientes.EventoCatClientesContactos;
			import mx.com.proquifa.proquifanet.rsl.vista.modelo.alertPqNet.ResultAlert;
			import mx.com.proquifa.proquifanet.rsl.vista.modelo.comun.Cliente;
			import mx.com.proquifa.proquifanet.rsl.vista.modelo.comun.Contacto;
			import mx.com.proquifa.proquifanet.rsl.vista.modelo.comun.Destinos;
			import mx.com.proquifa.proquifanet.rsl.vista.utils.DatosGlobales;
			import mx.com.proquifa.proquifanet.rsl.vista.utils.alertaSingleton;
			import mx.events.FlexEvent;
			import mx.messaging.ChannelSet;
			import mx.messaging.Producer;
			import mx.messaging.channels.AMFChannel;
			import mx.messaging.messages.AsyncMessage;
			
			import skins.catalogos.catalogoAlertas;
			
			private var arregloCubos:ArrayCollection;
			private var numCubosPorFila:uint;
			private var _dataProvider:ArrayCollection;
			private var _seAgrego:Boolean=false;
			
			public var isCurrentHabilitados:Boolean = true;
			
			private var productorActualiza:Producer = new Producer();
			private var productorBloquea:Producer = new Producer();
			
			private var canalAsociado:AMFChannel;
			private var canalesAsociado:ChannelSet;
			
			protected function reorganizarFilas():void
			{
				if(currentArray != null && currentArray.length > 0){
					
					if(numCubosPorFila >0){
						
						arregloCubos = new ArrayCollection;
						
						var filasCompletas:uint =int(currentArray.length / numCubosPorFila);
						
						var arrayTemp:Array = currentArray.source;
						var starIndex:uint;
						var tmp:Array;
						
						for (var i:int = 0; i < filasCompletas; i++) 
						{
							starIndex = i * numCubosPorFila;
							tmp = arrayTemp.slice(starIndex,starIndex + (numCubosPorFila));
							
							arregloCubos.addItem(tmp);
						}
						
						if(filasCompletas == 0)
						{
							starIndex = 0;
							tmp = arrayTemp.slice(starIndex,(currentArray.length));
							arregloCubos.addItem(tmp);
							
						}else{
							if((currentArray.length % numCubosPorFila) > 0 ){
								starIndex = starIndex + (numCubosPorFila);
								tmp = arrayTemp.slice(starIndex,(currentArray.length));
								arregloCubos.addItem(tmp);
							}
						}
						
						myList.dataProvider = arregloCubos;
					}
				}
			}
			
			private var currentWidth:int;
			public var currentCubosPorFila:int;
			override public function setActualSize(w:Number, h:Number):void{
				super.setActualSize(w,h);
				
				var sobrante:Number;
				if(currentWidth != w){
					currentWidth = w;			
					numCubosPorFila = int(w/255);
					
					sobrante = w%255;
					sobrante = sobrante/255;
					if(sobrante > 0.6){
						numCubosPorFila += 1;
					}
					
					if(currentCubosPorFila != numCubosPorFila){
						currentCubosPorFila = numCubosPorFila;
						reorganizarFilas();
					}
				}
			}
			
			private var currentArray:ArrayCollection;
			public function get dataProvider():ArrayCollection
			{
				return _dataProvider;
			}
			
			public function set dataProvider(value:ArrayCollection):void
			{
				myList.dataProvider = new ArrayCollection;
				_dataProvider = value;
				currentArray = _dataProvider;
				
				if(isCurrentHabilitados){
					var agregarContacto:Object = new Object;
					agregarContacto.CuboAgregar = "agregarContacto";
					currentArray.addItem(agregarContacto);
				}
				
				reorganizarFilas();
			}
			
			//============================================================TIENE QUE VER CON LA EDICIÓN DEL CONTACTO ============================================================
			
			public function set recibeClienteSeleccionado($cliente:Cliente):void{
				if( $cliente == null )
					return;
				_cliente = $cliente;
				obtenerContactosDelClienteSeleccionado();
			}
			
			private var _soloUnaVez:Boolean;
			private var _contacto:Contacto; 
			
			public function set recibeDelAlert( $dato:ResultAlert ):void{
				if( $dato == null || $dato.vieneD == null ){
					return;
				}
				if( _soloUnaVez ){
					if( $dato.vieneD == "catClienteAEliminarContacto" && $dato.botonSeleccionado == "SI"){
						_soloUnaVez = false;
						var evtElimina:EventoCatClientesContactos = new EventoCatClientesContactos( EventoCatClientesContactos.ELIMINAR_CONTACTO_SERVER_CLIENTE );
						evtElimina.idContacto = _contacto.idContacto;
						evtElimina.ventana = "CatClientes_Contacto deshabilita( idContacto:"+_contacto.idContacto+" )";
						dispatchEvent( evtElimina );
						producerMessage();
					}else if( $dato.vieneD == "catClienteAEditaContacto" && $dato.botonSeleccionado == "SI")
					{
						_soloUnaVez = false;
						if (_contacto.habilitado)
							_seAgrego = true;
						
						var evtEdita:EventoCatClientesContactos = new EventoCatClientesContactos( EventoCatClientesContactos.EDITAR_CONTACTO_SERVICE_CLIENTE );
						evtEdita.contacto = _contacto;
						evtEdita.contacto.origenRegistro = DatosGlobales.getInstance()._usuario.usuario;
						evtEdita.ventana = "CatClientes_Contacto ( idContacto:"+_contacto.idContacto+" )";
						dispatchEvent( evtEdita );
						producerMessage();
					}
				}
			}
			
			protected function producerMessage():void{
				var object:Object = new Object();
				object.body = 'actualizaSecciones';
				object.cliente = _cliente.idCliente;
				var mensaje:AsyncMessage = new AsyncMessage();
				mensaje.body = object;
				productorActualiza.send(mensaje);
			}
			
			private var _cliente:Cliente;
			
			private function obtenerContactosDelClienteSeleccionado():void{
				var evt:EventoCatClientesContactos = new EventoCatClientesContactos( EventoCatClientesContactos.OBTENER_CONTACTOS_CLIENTE );
				evt.idCliente = _cliente.idCliente;
				evt.habilitado = isCurrentHabilitados == true ? 1 : 0;
				dispatchEvent( evt );
			}
			
			private function obtenerContactosDelClienteAgregado():void{
				var evt:EventoCatClientesContactos = new EventoCatClientesContactos( EventoCatClientesContactos.OBTENER_CONTACTOS_CLIENTE );
				evt.idCliente = _cliente.idCliente;
				evt.habilitado = 1;
				dispatchEvent( evt );
			}
			
			protected function PanelContactosAnimado_creationCompleteHandler(event:FlexEvent):void
			{
				myList.addEventListener( EventoCatClientesContactos.MODIFICAR_CONTACTO_VISTA_CLIENTE , escucharModificarContacto, false,0,true);
				myList.addEventListener( EventoCatClientesContactos.AGREGAR_CONTACTO_VISTA_CLIENTE, agregaNuevoContacto,false,0,true);
				myList.addEventListener( EventoCatClientesContactos.ELIMINAR_CONTACTO_VISTA_CLIENTE,escucharEliminarContacto,false,0,true);
				producerInit();
			}
			
			protected function producerInit():void
			{
				productorActualiza.destination = "catalogoClienteActualizaSecciones";
				
				this.canalAsociado = new AMFChannel("my-polling-amf", Destinos.END_POINT_MENSAJERIA);
				this.canalesAsociado = new ChannelSet();
				this.canalesAsociado.addChannel(canalAsociado);
				productorActualiza.channelSet = this.canalesAsociado;
			}
			
			private function escucharEliminarContacto(e:EventoCatClientesContactos):void
			{
				_soloUnaVez = true;
				_contacto = e.contacto;
				alertaSingleton.show( '¿Desea deshabilitar el contacto?' ,catalogoAlertas.TIT_ADV_atencion,'catClienteAEliminarContacto','','','SI','NO');
			}
			
			private function escucharModificarContacto(eve:EventoCatClientesContactos):void
			{
				_contacto = eve.contacto;
				_soloUnaVez = true;
				alertaSingleton.show( '¿Desea modificar el contacto?' ,catalogoAlertas.TIT_ADV_atencion,'catClienteAEditaContacto','','','SI','NO');
			}
			
			private function agregaNuevoContacto(evt:EventoCatClientesContactos):void{
				
				var evtAgrega:EventoCatClientesContactos = new EventoCatClientesContactos( EventoCatClientesContactos.AGREGA_CONTACTO_SERVICE_CLIENTE );
				evtAgrega.tipo = 'inserta';
				evtAgrega.contacto = evt.contacto;
				evtAgrega.contacto.tipo = 'Clientes';
				evtAgrega.contacto.empresa = _cliente.nombre;
				evtAgrega.contacto.idEmpresa = _cliente.idCliente;
				evtAgrega.contacto.origenRegistro = DatosGlobales.getInstance()._usuario.usuario;
				evtAgrega.ventana = "CatClientes_Contacto ( idContacto:"+evt.contacto.idContacto+" )";
				dispatchEvent( evtAgrega ); 
				producerMessage();
			}
			
			public function set confirmaNuevoContacto( $dato:Number):void
			{
				if( isNaN( $dato ) || $dato < 0 ){
					return;
				} 
				obtenerContactosDelClienteSeleccionado();
			}
			
			public function set confirmaModificacionAContacto( $dato:Boolean ):void{
				if( !$dato ){
					return;
				}
				if (_seAgrego)
				{
					_seAgrego = false;
					obtenerContactosDelClienteAgregado();
				}
				
				obtenerContactosDelClienteSeleccionado();
				alertaSingleton.show('El contacto fue modificado.', catalogoAlertas.TIT_CONF_operacionExitosa );
			}
			
			public function set confirmaEliminacionDeContacto( $dato:Boolean ):void{
				if( !$dato ){
					return;
				}
				obtenerContactosDelClienteSeleccionado();
				alertaSingleton.show('El contacto fue deshabilitado.', catalogoAlertas.TIT_CONF_operacionExitosa );
			}
			
		]]>
	</fx:Script>
	
	<s:List id="myList" height="100%" width="100%" contentBackgroundAlpha="0" borderVisible="false"	horizontalScrollPolicy="off" 
			itemRenderer="mx.com.proquifa.proquifanet.rsl.vista.vistas.catalogos.clientes.itemRenderer.empresasGeneral.ItemRendererPanelContactosAnimado">
	</s:List>
</s:Group>
