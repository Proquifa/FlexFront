<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 width="100%" height="100%" 
		 >
	<fx:Declarations>
		<s:Resize id="fxResizeCompare" duration="400" effectEnd="fxResizeCompareEndHandler(event)" />
		<s:Fade id="fxFadeCompare" duration="150" effectEnd="fxFadeEndHandler(event)" />
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import mx.core.IVisualElement;
			import mx.events.EffectEvent;
			
			import skins.catalogos.catalogoIconosBotones;
			
			private var _espacioA:IVisualElement;
			private var _espacioB:IVisualElement;
			public const _TABLA_GRID:String = "tablaGrid";
			public const _GRAFICAS:String = "graficaChart";
			public var vistaSelected:String = _TABLA_GRID;
			
			public function get espacioB():IVisualElement
			{
				return _espacioB;
			}
			
			public function set espacioB(value:IVisualElement):void
			{
				_espacioB = value;
				if( value != null )
				{
					if( grpEspacioB.numChildren > 0 )
					{
						grpEspacioB.removeAllElements();
					}
					grpEspacioB.addElement(_espacioB);
				}
			}
			
			public function get espacioA():IVisualElement
			{
				return _espacioA;
			}
			
			public function set espacioA(value:IVisualElement):void
			{
				if(value != null)
				{
					_espacioA = value;
					
					if(grpEspacioA.numChildren > 1)
						grpEspacioA.removeElementAt(1);
					
					grpEspacioA.percentWidth = 100;
					grpEspacioA.percentHeight = 100;
					grpEspacioA.addElementAt(_espacioA,1);
					
				}
			}
			
			public function compararVistas():void
			{
				if( fxFadeCompare.isPlaying )
					fxFadeCompare.stop();
				if( fxResizeCompare.isPlaying )
					fxResizeCompare.stop();
				
				fxFadeCompare.target = espacioA;
				fxResizeCompare.target = grpEspacioA;
				
				if( vistaSelected == _TABLA_GRID )
				{
					fxResizeCompare.startDelay = 200;
					fxFadeCompare.startDelay = 0;
					fxResizeCompare.widthTo = 0;
					fxFadeCompare.alphaTo = 0;
					imgTab.source= catalogoIconosBotones.getInstance().tabGrid;
				}
				else
				{
					fxResizeCompare.startDelay = 0;
					fxResizeCompare.widthTo = width;
					fxFadeCompare.startDelay = 450;
					fxFadeCompare.alphaTo = 1;
					grpEspacioA.width = 1;
					imgTab.source = catalogoIconosBotones.getInstance().tabGrafica;
					grpEspacioA.height = height;
					grpEspacioA.visible = grpEspacioA.includeInLayout = true;
				}
				fxFadeCompare.play();
				fxResizeCompare.play();
			} 
			
			protected function fxResizeCompareEndHandler( event:EffectEvent ):void
			{
				if( vistaSelected == _TABLA_GRID )
				{
					vistaSelected = _GRAFICAS;
					espacioB.percentWidth = espacioB.percentHeight = 100;
					grpEspacioA.visible = grpEspacioA.includeInLayout = false;
				}
				else
				{
					vistaSelected = _TABLA_GRID;
					grpEspacioA.visible = grpEspacioA.includeInLayout = true;
					grpEspacioA.percentWidth = grpEspacioA.percentHeight = 100;
					espacioA.visible = espacioA.includeInLayout = true;
				}
			}
			
			protected function fxFadeEndHandler(event:EffectEvent):void
			{
				if( fxFadeCompare.alphaTo == 1 ){
					espacioA.visible = espacioA.includeInLayout = true;
					espacioA.percentWidth = espacioA.percentHeight = 100;
				}
				else
				{
					fxFadeCompare.target.includeInLayout = false;
				}
			}
		]]>
	</fx:Script>
	<!-- LAYER 1 -->
	<s:HGroup width="100%" height="100%" gap="0" depth="500">
		<s:Group id="grpEspacioA" width="100%" height="100%">
			<s:Rect width="100%" height="100%" left="10">
				<s:fill>
					<s:SolidColor color="white"/>
				</s:fill>
			</s:Rect>
			<!-- AQUI SE AGREGA espacioA-->
		</s:Group>
		<s:Group width="0" height="100%"  id="btnGraficaTabla">
			<s:Image source="{ catalogoIconosBotones.getInstance().tabGrafica }" horizontalCenter="-5" verticalCenter="-230" id="imgTab" depth="600"/><!---230-->
		</s:Group>
	</s:HGroup>
	<s:VGroup id="grpEspacioB" width="100%" height="100%" paddingLeft="10" depth="0"/>
	<!-- LAYER 2 -->
	<!-- AQUI SE AGREGA espacioB -->
</s:Group>
